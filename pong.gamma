external_fn SDL_SetMainReady(): void
external_fn SDL_Init(flags: u32): i32
external_fn SDL_Quit(): void

external_fn SDL_CreateWindow(title: ^u8, x: i32, y: i32, w: i32, h: i32, flags: u32): ^void
external_fn SDL_DestroyWindow(window: ^void): void

external_fn SDL_Delay(ms: u32): void

external_fn SDL_PollEvent(event: ^SDL_Event): i32

external_fn SDL_CreateRenderer(window: ^void, index: i32, flags: u32): ^void
external_fn SDL_DestroyRenderer(renderer: ^void): void
external_fn SDL_SetRenderDrawColor(renderer: ^void, r: u8, g: u8, b: u8, a: u8): i32
external_fn SDL_RenderClear(renderer: ^void): i32
external_fn SDL_RenderPresent(renderer: ^void): void

layout SDL_Event
  type: 0 u32
  timestamp: 4 u32
  window_event_id: 12 u32

  # Make sure that the size of the struct matches the original
  padding: 0 [56]u8
end

fn main(): i32
  let EVENT_QUIT: u32 = (cast u32 256)
  let EVENT_WINDOW: u32 = (cast u32 512)
  let WINDOW_CLOSE: u32 = (cast u32 14)
  let INIT_EVERYTHING: u32 = (cast u32 62001)
  let NULL: ^void = (cast ^void 0)

  (SDL_SetMainReady)

  let status: i32 = (SDL_Init INIT_EVERYTHING)

  let window: ^void = (SDL_CreateWindow (cast ^u8 0) 
                                        (cast i32 1) 
                                        (cast i32 1) 
                                        (cast i32 800) 
                                        (cast i32 800) 
                                        (cast u32 0))

  let renderer: ^void = (SDL_CreateRenderer window (cast i32 -1) (cast u32 0))
  loop @game_loop
    let event: SDL_Event
    loop
      let has_more_events: i32 = (SDL_PollEvent (& event))
      if (not (cast bool has_more_events))
        break
      end

      if (eq event.type EVENT_QUIT)
        break @game_loop
      end
    end

    (SDL_SetRenderDrawColor renderer (cast u8 255) 
                                     (cast u8 0)
                                     (cast u8 0) 
                                     (cast u8 0))
    (SDL_RenderClear renderer)
    (SDL_RenderPresent renderer)
  end

  (SDL_DestroyRenderer renderer)
  (SDL_DestroyWindow window)
  (SDL_Quit)
  
  return (cast i32 0)
end
